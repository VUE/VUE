plugins {

    id "name.remal.check-dependency-updates" version "1.1.4"

    // Apply Shadow so we can make a bundled jar
    id 'com.github.johnrengelman.shadow' version '6.1.0'

    // For Windows .exe files
    id "edu.sc.seis.launch4j" version "2.4.9"

    // Apply the java plugin to add support for Java - explicit so that Shadow works
    id "java"

    // Apply the application plugin to add support for building an application
    id "application"

//    id "com.github.spotbugs" version "4.6.0"
}


repositories {
    mavenCentral()
}

group = "edu.tufts.vue"
applicationName = 'VUE'

// Define the main class for the application
mainClassName = 'tufts.vue.VUE'


// Load the version number from the main resources file.
def resourceProps = file('src/main/resources/tufts/vue/VueResources.properties').withReader {
    reader ->
        def props = new Properties()
        props.load(reader)
        props
}
version = resourceProps['vue.version']


sourceCompatibility = 1.8
targetCompatibility = 1.8


// java {
//     withJavadocJar()
// }


jar {
    manifest {
        attributes 'Implementation-Title': project.name
        attributes 'Implementation-Version': project.version
        attributes 'Main-Class': project.mainClassName
    }
}

launch4j {
    mainClassName = project.mainClassName
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/" + project.tasks.shadowJar.archiveName
    downloadUrl = 'https://adoptium.net/'
    icon = "${projectDir}/Nullsoft-Installer/VUE.ico"
    if (hasProperty("console")) {
        headerType = "console"
    }
}

createAllExecutables.dependsOn(shadowJar)

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

dependencies {
    // logging: manually specify these to override deps
    implementation 'org.slf4j:slf4j-api:1.7.16'
    runtimeOnly 'org.slf4j:slf4j-log4j12:1.7.16'

    // javax/jakarta
    implementation 'jakarta.activation:jakarta.activation-api:1.2.2'
    runtimeOnly 'com.sun.activation:jakarta.activation:1.2.2'

    implementation 'jakarta.xml.rpc:jakarta.xml.rpc-api:1.1.4'
    runtimeOnly 'com.sun.xml.rpc:jaxrpc-impl:1.1.+'
    runtimeOnly 'com.sun.xml.rpc:jaxrpc-spi:1.1.+'

    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:2.3.+'

    implementation 'javax.xml:jax-qname:1.1'

    implementation 'javax.xml.soap:saaj-api:1.3.5' // now included, but deprecated in java 9
    runtimeOnly 'com.sun.xml.messaging.saaj:saaj-impl:1.3.+'

    // Castor data-binding framework
    implementation 'org.codehaus.castor:castor-xml:1.4.1'
    implementation 'org.codehaus.castor:castor:1.4.1'

    // Nothing OSID-related was found.
    implementation files('lib/okiSID_rc6_1.jar')
    implementation files('lib/okiOSID-2.0a.jar')
    implementation files('lib/OsidProvider.jar')
    implementation files('lib/okiSIDimpl.jar')
    implementation files('lib/OSIDLoggingPlain-1.0.jar')
    implementation files('lib/XmlfileRepositoryOSID-1.0.jar')
    implementation files('lib/OsidRegistry.jar')
    implementation files('lib/OSIDIdNoPersist-1.0.jar')

    runtimeOnly 'com.ibm.icu:icu4j:74.1'

    // Apache Axis
    implementation 'axis:axis-ant:1.4'
    implementation 'axis:axis:1.4'

    // Apache Batik
    implementation 'org.apache.xmlgraphics:batik-dom:1.8'
    implementation 'org.apache.xmlgraphics:batik-svggen:1.8'

    // Apache Commons
    implementation 'commons-httpclient:commons-httpclient:3.1'
    // TODO upgrade https://mvnrepository.com/artifact/org.apache.httpcomponents/httpclient
    implementation 'commons-lang:commons-lang:2.4'
    implementation 'commons-codec:commons-codec:1.3'
    implementation 'commons-discovery:commons-discovery:0.2'
    implementation 'commons-logging:commons-logging:1.2'
    implementation files('lib/apache-commons.jar')
    // TODO this is some really old 'commons-net:commons-net:1.1.0' with extra classes I can't find anywhere else.

    // Apache FOP (Formatting Objects Processor)
    implementation 'fop:fop:0.20.5'
    // TODO upgrade https://mvnrepository.com/artifact/org.apache.xmlgraphics/fop

    // Apache Tomcat
    implementation 'tomcat:servlet:4.0.6'

    // Apache Xalan
    implementation 'xalan:xalan:2.7.+'

    // Apache Xerces
    runtimeOnly 'xerces:xercesImpl:2.12.+'

    // Apache XML-RPC
    implementation 'xmlrpc:xmlrpc:1.1' // TODO upgrade

    // Jena - TODO migrate to apache jena org.apache.jena/jena
    implementation 'com.hp.hpl.jena:jena:2.6.4'
    implementation 'com.hp.hpl.jena:json-jena:1.0'
    implementation 'com.hp.hpl.jena:arq:2.+'

    // TODO Guava now contains google-collect, but can't use it because com.google.common.collect.AbstractMapEntry is monkeyed with
    implementation files('lib/google-collect-snapshot-20090211.jar')
    // newer than last released snapshot?

    // Misc deps from repository
    implementation 'au.com.bytecode:opencsv:2.4'

    // for PDF export, presentation notes
    implementation('com.lowagie:itext:2.1.7') {
        exclude group: 'bouncycastle', module: 'bcmail-jdk14'
        exclude group: 'bouncycastle', module: 'bcprov-jdk14'
        exclude group: 'bouncycastle', module: 'bctsp-jdk14'
    }
    constraints {
        implementation('bouncycastle:bcmail-jdk15to18:166') {
            because 'version 138 is not available'
        }
        implementation('bouncycastle:bcprov-jdk15to18:166') {
            because 'version 138 is not available'
        }
        implementation('bouncycastle:bctsp-jdk15to18:166') {
            because 'version 138 is not available'
        }
    }
    implementation 'com.miglayout:miglayout:3.7.4'
    implementation 'cryptix:cryptix:3.2.0'
    implementation 'javatar:javatar:2.5'
    implementation 'org.jdom:jdom-legacy:1.1.3'
    implementation 'log4j:log4j:1.2.17'
    implementation 'net.java.dev.jgoodies:looks:2.1.4'
    implementation 'net.java.linoleum:jlfgr:1_0'
    implementation 'org.beanshell:bsh-core:2.0b4'
    implementation 'org.netbeans.external:jakarta-oro-2.0.8:RELEASE69'
    implementation 'org.twitter4j:twitter4j-core:2.1.+'
    implementation 'urbanophile:java-getopt:1.0.9'
    
    // TODO upgrade to https://mvnrepository.com/artifact/com.rometools/rome
    implementation 'rome:rome:0.9'

    // Needed for text blocks - see #4
    implementation group: 'gnu-regexp', name: 'gnu-regexp', version: '1.1.4'

    // Tufts-related
    implementation files('lib/TuftsOsidProvider.jar')

    // MIT-related
    implementation files('lib/MIT-OTS-NO_PROVIDER.jar')
    implementation files('lib/MITAuthenticationTypes.jar')
    implementation files('lib/MITRepositoryTypes.jar')
    implementation files('lib/MITSearchTypes.jar')

    // macOS related
    runtimeOnly files('lib/quaqua.jar') // v 7.4.1 not found, but others are out there.
    implementation files('lib/apple-laf.jar')

    // Internal
    implementation files('lib/VUE-Java15.jar')
    implementation files('lib/VUE-MacOSX.jar')

    // Referenced only in strings, I think...
    // implementation files('lib/jhapi.jar') // com.harvestroad.hive not found

    // Misc deps from local lib directory
    implementation files('lib/AlchemyAPI.jar') // not found in the right namespace
    implementation files('lib/calais.jar') // not found

    implementation files('lib/fedora-client_2_2.jar')
    implementation files('lib/fedoragentypes.jar')

    implementation files('lib/gdata-client-1.0.jar')
    // TODO migrate https://github.com/google/gdata-java-client/blob/wiki/MigratingToGoogleApiJavaClient.md
    implementation files('lib/gdata-core-1.0.jar')
    implementation files('lib/googleapi.jar') // TODO google soap API deprecated

    implementation files('lib/jhbasic_unsigned.jar')
    // couldn't find anything with spec 1.0 (impl 1.1.2_01)
    implementation files('lib/joscar-0.9.3-bin.jar') // TODO remove - AIM is no longer

    implementation files('lib/jxtaid.jar')
    // couldn't find - maybe https://mvnrepository.com/artifact/net.jxta/jxta-jxse ?
    implementation files('lib/jxtasecurity.jar') // couldn't find

    // TODO some included in java now? but more errors when omitted
    implementation files('lib/saxon6.5.jar')

    // TODO GPL2 upstream plus classes without sources
    implementation files('lib/simplyhtml2.jar') // couldn't find
    implementation files('lib/splash.jar') // couldn't find
    implementation files('lib/trippi-1.1.2-core.jar') // TODO upgrade?
}
